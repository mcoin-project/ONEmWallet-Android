apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.3'

        defaultConfig {
            testInstrumentationRunner = 'android.support.test.runner.AndroidJUnitRunner'
            applicationId = 'com.onem.onemwallet'
            minSdkVersion.apiLevel = 23
            targetSdkVersion.apiLevel = 26
            versionCode = 4
            // versionName = "Portvizitiu"  // A random wallet synonym in a random language (RO here)
            // versionName = "Billetera"  // ES
            // versionName = "Portefeuille"  // FR
            versionName = "Cartera"  // Catalan
        }

        productFlavors.with {
            create("onem") {
                applicationId "com.onem.onemwallet"
                resValues.with {
                    create() {
                        type = "string"
                        name = "app_name"
                        value = "ONEmWallet"
                    }
                }
            }
            create("demo") {
                applicationId "com.onem.onemdemo"
                resValues.with {
                    create() {
                        type = "string"
                        name = "app_name"
                        value = "ONEmWallet-demo"
                    }
                }
            }

        }

        ndk {
            platformVersion = "23"
            // TODO: remove abiFilters once NDK fixes MIPS compile issue
            // clang crashes when building for MIPS at least since NDK 14.0.3529234 rc1
            // https://github.com/android-ndk/ndk/issues/290
            // https://github.com/android-ndk/ndk/issues/314
            // https://github.com/ntrrgc/jsvm/commit/f7c71cfd6c76dcad67aa1e0638705ad377ee9938
            abiFilters.addAll(['x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'])
            moduleName = "core"
            toolchain = "clang"

            CFlags.addAll(['-std=c99', '-g', '-DBITCOIN_TESTNET=0', '-I' + file('src/main/secp/secp256k1/include'),
                           '-I' + file('src/main/secp/secp256k1'), '-I' + file('src/main/secp'), '-I' + file('src/main/jni/loafwallet-core'),
                           '-I' + file('src/main/jni/transition'), '-Wno-trigraphs', /**'-Wmissing',*/
                           '-Wno-missing-prototypes', '-Werror=return-type',
                           '-Wdocumentation', '-Wunreachable-code-aggressive', '-Wno-missing-braces', '-Wparentheses', '-Wswitch', '-Wno-unused-function',
                           '-Wunused-label', '-Wno-unused-parameter', '-Wunused-variable', '-Wunused-value', '-Wempty-body',
                           '-Wconditional-uninitialized', '-Wno-unknown-pragmas', '-pedantic', '-Wshadow', '-Wfour-char-constants',
                           '-Wno-conversion', '-Wconstant-conversion', '-Wint-conversion', '-Wbool-conversion ', '-Wenum-conversion',
                           '-Wassign-enum', '-Wno-shorten-64-to-32', '-Wpointer-sign', '-Wnewline-eof', '-Wdeprecated-declarations', '-Wno-sign-conversion'])
            ldLibs.addAll(['android', 'log'])
            stl = 'gnustl_static'
        }

        lintOptions.with {
            // set to true to turn off analysis progress reporting by lint
            quiet = false
            explainIssues = true
            // if true, stop the gradle build if errors are found
            abortOnError = true
            // if true, only report errors
            ignoreWarnings = false

            disable 'MissingTranslation'
        }

        buildTypes {
            gitUpdateModules()
            release {
                proguardFiles.add(file('proguard-rules.pro'))
                minifyEnabled = false
                debuggable = false

                buildConfigFields.with {
                    create() {
                        type = "boolean"
                        name = "BITCOIN_TESTNET"
                        value = "false"
                    }
                }
                ndk {
                    CFlags.addAll(['-DBITCOIN_TESTNET=0'])
                    minifyEnabled = false
                    debuggable = false
                }
            }
            debug {
                proguardFiles.add(file('proguard-rules.pro'))
                debuggable = true
                minifyEnabled = false
                buildConfigFields.with {
                    create() {
                        type = "boolean"
                        name = "BITCOIN_TESTNET"
                        value = "true"
                    }
                }
                ndk {
                    CFlags.addAll(['-DBITCOIN_TESTNET=1'])
                    minifyEnabled = false
                    debuggable = true
                }
            }

        }
    }
}

def gitUpdateModules() {
    ['sh', '-c', 'git submodule foreach git pull origin master'].execute().text.trim()
    getLogger().debug("gitUpdateModules done!")
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-io:commons-io:2.0.1'
    compile 'org.eclipse.jetty:jetty-webapp:9.2.19.v20160908'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.2.19.v20160908'
    compile 'org.eclipse.jetty:jetty-continuation:9.2.19.v20160908'
    compile 'org.slf4j:slf4j-api:1.7.22'
    //    compile 'org.apache.commons:commons-compress:1.8.1'
    //    compile group: 'org.apache.ant', name: 'ant', version: '1.8.2'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.google.android.gms:play-services-vision:11.4.2'
    compile 'com.google.firebase:firebase-core:11.4.2'
    compile 'com.google.firebase:firebase-crash:11.4.2'
    compile 'io.sigpipe:jbsdiff:1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}
apply plugin: 'com.google.gms.google-services'
